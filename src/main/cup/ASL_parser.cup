package asl.input;

import java_cup.runtime.*;
import asl.input.ASLLexer;
import asl.input.ASLConsumer;
import asl.model.core.*;
import asl.model.core.functions.*;
import asl.model.system.ASLObjectsFactory;
import asl.model.system.SequenceFacade;
import java.util.ArrayList;
import java.util.Arrays;
import java.util.List;

scan with {: return super.scan(); :};

parser code {:
  ASLConsumer consumer;
  public ASLParser(Scanner lex, ASLConsumer consumer_) {
    super(lex, new ComplexSymbolFactory());
    consumer = consumer_;
  }

  private static List<ASLObject> makeList(ASLObject... args) {
    return new ArrayList<>(Arrays.asList(args));
  }
:};


/* ------------Declaration of Terminals and Non Terminals Section----------- */

terminal _IF;
terminal _THEN;
terminal _ELSE;
terminal _LCURL;
terminal _RCURL;
terminal _SEMICOLON;
terminal _COMMA;
terminal _LPAR;
terminal _RPAR;
terminal _LBR;
terminal _RBR;
terminal _STAR;
terminal _SLASH;
terminal _PERCENT;
terminal _PLUS;
terminal _MINUS;
terminal _EQ;
terminal _DOT;
terminal _GT;
terminal _GTE;
terminal _LT;
terminal _LTE;
terminal _EQQ;
terminal _NEQ;
terminal _AND;
terminal _OR;
terminal _NOT;
terminal UMINUS;

terminal Undef UNDEF;
terminal IntegerAtom INTCONST;
terminal DoubleAtom DOUBLECONST;
terminal StringAtom STRING;
terminal BooleanAtom TRUE, FALSE;
terminal Variable VAR, ATTRVAR;
terminal QNameAtom QNAME;

non terminal program;
non terminal List<ASLObject>
    dot_expr,
    br_expr,
    link_expr,
    one_branch_cond,
    condition,
    st_list,
    st_list_opt,
    arg_commalist,
    arg_commalist_opt,
    plus_expr,
    mult_expr,
    and_expr,
    or_expr;
non terminal ASLObject statement, expr, progn_expr;


/* -------------Precedence and Associatively of Terminals Section----------- */

precedence right _EQ;
precedence right _IF, _THEN, _ELSE;
precedence left _OR;
precedence left _AND;
precedence left _EQQ, _NEQ;
precedence left _GT, _GTE, _LT, _LTE;
precedence left _PLUS, _MINUS;
precedence left _STAR, _SLASH, _PERCENT;
precedence left _DOT, _LBR;
precedence right _NOT, UMINUS;


/* ----------------------------Grammar Section-------------------- */
   
program ::=
    /* empty */
    | program statement:stmt                          {: consumer.consume(stmt); :}
    ;

statement ::=
    expr:e _SEMICOLON                                 {: RESULT = e; :}
    | progn_expr:p_e                                  {: RESULT = p_e; :}
    ;

progn_expr ::=
    _LCURL st_list_opt:list _RCURL                    {: RESULT = new PrognFunction(list); :}
    | _LCURL expr:e _RCURL                            {: RESULT = new PrognFunction(makeList(e)); :}
    ;

st_list_opt ::=
    st_list:list                                      {: RESULT = list; :}
    | st_list:list expr:e                             {: list.add(e); RESULT = list; :}
    | /* empty */                                     {: RESULT = makeList(); :}
    ;

st_list ::=
    st_list:list statement:e                          {: list.add(e); RESULT = list; :}
    | statement:e                                     {: RESULT = makeList(e); :}
    ;

expr ::=
    VAR:v                                             {: RESULT = v; :}
    | VAR:lhs _EQ expr:rhs                            {: RESULT = new SetqFunction(List.of(lhs, rhs)); :}
    | link_expr:list                                  {: RESULT = new ArefFunction(list); :}
    | link_expr:list _EQ expr:e                       {: list.add(e); RESULT = new AsetFunction(list); :}
    | _LPAR expr:e _RPAR                              {: RESULT = e; :}
    | _LPAR progn_expr:e _RPAR                        {: RESULT = e; :}
    | QNAME:fname _LPAR arg_commalist_opt:al _RPAR    {: RESULT = ASLObjectsFactory.makeFunctionCall(fname.value(), al); :}
    | INTCONST:atom                                   {: RESULT = atom; :}
    | DOUBLECONST:atom                                {: RESULT = atom; :}
    | STRING:atom                                     {: RESULT = atom; :}
    | TRUE:atom                                       {: RESULT = atom; :}
    | FALSE:atom                                      {: RESULT = atom; :}
    | UNDEF:und                                       {: RESULT = und; :}
    | QNAME:atom                                      {: RESULT = atom; :}
    | condition:args                                  {: RESULT = new CondFunction(args); :}
    | and_expr:list                                   {: RESULT = new AndFunction(list); :}
    | or_expr:list                                    {: RESULT = new OrFunction(list); :}
    | expr:e0 _SLASH    expr:e1                       {: RESULT = new DivFunction(List.of(e0, e1)); :}
    | expr:e0 _PERCENT  expr:e1                       {: RESULT = new ModFunction(List.of(e0, e1)); :}
    | plus_expr:list                                  {: RESULT = new AddFunction(list); :}
    | expr:e0 _MINUS    expr:e1                       {: RESULT = new SubFunction(List.of(e0, e1)); :}
    | mult_expr:list                                  {: RESULT = new MulFunction(list); :}
    | expr:e0 _GT       expr:e1                       {: RESULT = new GtFunction(List.of(e0, e1)); :}
    | expr:e0 _GTE      expr:e1                       {: RESULT = new GteFunction(List.of(e0, e1)); :}
    | expr:e0 _LT       expr:e1                       {: RESULT = new LtFunction(List.of(e0, e1)); :}
    | expr:e0 _LTE      expr:e1                       {: RESULT = new LteFunction(List.of(e0, e1)); :}
    | _MINUS:op expr:e0                               {: RESULT = new UMinusFunction(List.of(e0)); :} %prec UMINUS
    | _NOT:op expr:e0                                 {: RESULT = new NotFunction(List.of(e0)); :}
    ;

arg_commalist_opt ::=
    arg_commalist:al                                  {: RESULT = al; :}
    | /* empty */                                     {: RESULT = makeList(); :}
    ;

arg_commalist ::=
    arg_commalist:list _COMMA expr:a                  {: list.add(a); RESULT = list; :}
    | expr:a                                          {: RESULT = makeList(a); :}
    ;

plus_expr ::=
    plus_expr:list _PLUS expr:e                       {: list.add(e); RESULT = list; :}
    | expr:e0      _PLUS expr:e1                      {: RESULT = makeList(e0, e1); :}
    ;

mult_expr ::=
    mult_expr:list _STAR expr:e                       {: list.add(e); RESULT = list; :}
    | expr:e0      _STAR expr:e1                      {: RESULT = makeList(e0, e1); :}
    ;

and_expr ::=
    and_expr:list  _AND expr:e                        {: list.add(e); RESULT = list; :}
    | expr:e0      _AND expr:e1                       {: RESULT = makeList(e0, e1); :}
    ;

or_expr ::=
    or_expr:list   _OR expr:e                         {: list.add(e); RESULT = list; :}
    | expr:e0      _OR expr:e1                        {: RESULT = makeList(e0, e1); :}
    ;

dot_expr ::=
    dot_expr:list  _DOT expr:attr                     {: list.add(attr); RESULT = list; :}
    | VAR:var      _DOT expr:attr                     {: RESULT = makeList(var, attr); :}
    ;

br_expr ::=
    dot_expr:list  _LBR expr:attr _RBR                {: list.add(attr); RESULT = list; :}
    | br_expr:list _LBR expr:attr _RBR                {: list.add(attr); RESULT = list; :}
    | VAR:var      _LBR expr:attr _RBR                {: RESULT = makeList(var, attr); :}
    ;

link_expr ::=
    dot_expr:list                                     {: RESULT = list; :}
    | br_expr:list                                    {: RESULT = list; :}
    ;

one_branch_cond ::=
    _IF expr:e0 _THEN expr:e1                         {: RESULT = makeList(e0, e1); :}
    ;

condition ::=
    one_branch_cond:args                              {: RESULT = args; :}
    | one_branch_cond:args _ELSE expr:e               {: args.add(e); RESULT = args; :}
    ;
