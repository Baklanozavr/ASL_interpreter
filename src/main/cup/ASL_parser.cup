package asl.input;

import java_cup.runtime.*;
import asl.input.ASLLexer;
import asl.input.ASLConsumer;
import asl.model.core.*;
import asl.model.core.functions.*;
import asl.model.system.ASLObjectsFactory;
import asl.model.system.SequenceFacade;
import java.util.List;

scan with {: return super.scan(); :};

parser code {:
  ASLConsumer consumer;
  public ASLParser(Scanner lex, ASLConsumer consumer_) {
    super(lex, new ComplexSymbolFactory());
    consumer = consumer_;
  }
:};


/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal _SEMICOLON;
terminal _COMMA;
terminal _LPAR;
terminal _RPAR;
terminal _STAR;
terminal _SLASH;
terminal _PLUS;
terminal _MINUS;
terminal _EQ;
terminal UMINUS;

terminal Undef UNDEF;
terminal IntegerAtom INTCONST;
terminal DoubleAtom DOUBLECONST;
terminal BooleanAtom TRUE, FALSE;
terminal Variable VAR, ATTRVAR;
terminal QNameAtom QNAME;

non terminal program;
non terminal List<ASLObject> arg_commalist, arg_commalist_opt;
non terminal ASLObject statement, expr, arg;


/* -------------Precedence and Associatively of Terminals Section----------- */

precedence left _PLUS, _MINUS, _STAR, _SLASH;
precedence right _EQ, UMINUS;


/* ----------------------------Grammar Section-------------------- */
   
program   ::=
    /* empty */
    | program statement:stmt                          {: consumer.consume(stmt); :}
    ;

statement ::=
    VAR:lhs _EQ expr:rhs _SEMICOLON                   {: RESULT = new SetqFunction(List.of(lhs, rhs)); :}
    | expr:e _SEMICOLON                               {: RESULT = e; :}
    ;

expr ::=
    VAR:v                                             {: RESULT = v; :}
    | _LPAR expr:e _RPAR                              {: RESULT = e; :}
    | QNAME:fname _LPAR arg_commalist_opt:al _RPAR    {: RESULT = ASLObjectsFactory.makeFunctionCall(fname.value(), al); :}
    | INTCONST:atom                                   {: RESULT = atom; :}
    | DOUBLECONST:atom                                {: RESULT = atom; :}
    | TRUE:atom                                       {: RESULT = atom; :}
    | FALSE:atom                                      {: RESULT = atom; :}
    | UNDEF:und                                       {: RESULT = und; :}
    | QNAME:atom                                      {: RESULT = atom; :}
    | expr:e0 _SLASH    expr:e1                       {: RESULT = ASLObjectsFactory.makeFunctionCall ("div"    , e0, e1); :}
    | expr:e0 _PLUS     expr:e1                       {: RESULT = new AddFunction(List.of(e0, e1)); :}
    | expr:e0 _MINUS    expr:e1                       {: RESULT = ASLObjectsFactory.makeFunctionCall ("sub"    , e0, e1); :}
    | expr:e0 _STAR     expr:e1                       {: RESULT = ASLObjectsFactory.makeFunctionCall ("mul"    , e0, e1); :}
    | _MINUS:op expr:e0                               {: RESULT = ASLObjectsFactory.makeFunctionCall ("uminus" , e0); :} %prec UMINUS
    ;

arg_commalist_opt  ::=
    arg_commalist:al                                  {: RESULT = al; :}
    | /* empty */                                     {: RESULT = ASLObjectsFactory.makeSequence(); :}
    ;

arg_commalist  ::=
    arg_commalist:list _COMMA arg:a                   {: list.add(a); RESULT = list; :}
    | arg:a                                           {: RESULT = ASLObjectsFactory.makeSequence(a); :}
    ;

arg ::=
    expr:e                                            {: RESULT = e; :}
    ;
